
!include "MUI2.nsh"

!macro SetContext
!define ID ${__LINE__}
ClearErrors
UserInfo::GetName
IfErrors default_${ID}
Pop $0
UserInfo::GetAccountType
Pop $1
StrCmp $1 "Admin" 0 default_${ID}
SetShellVarContext all
Goto done_${ID}
default_${ID}:
SetShellVarContext current
done_${ID}:
!undef ID
!macroend

Var StartMenuFolder

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "COPYING"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_STARTMENU Startmenu $StartMenuFolder
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_RUN "$INSTDIR\crerecembl.exe"
!define MUI_FINISHPAGE_RUN_NOTCHECKED
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH 

!insertmacro MUI_LANGUAGE "English"

Name "@PACKAGE_NAME@"
Outfile "@PACKAGE_NAME@-@PACKAGE_VERSION@_win32_installer.exe"
InstallDir "$PROGRAMFILES\@PACKAGE_NAME@-@PACKAGE_VERSION@"

Section "Install"
SetOutPath "$INSTDIR"
!insertmacro SetContext
@CREATEDIR_LIST@
@INST_LIST@
WriteUninstaller "$INSTDIR\Uninstall.exe"

!insertmacro MUI_STARTMENU_WRITE_BEGIN Startmenu
CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
CreateShortCut "$SMPROGRAMS\$StartMenuFolder\@PACKAGE_NAME@.lnk" "$INSTDIR\crerecembl.exe"
CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
!insertmacro MUI_STARTMENU_WRITE_END

WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" "DisplayName" "@PACKAGE_NAME@" 
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" "UninstallString" "$\"$INSTDIR\Uninstall.exe$\"" 
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" "QuietUninstallString" "$\"$INSTDIR\Uninstall.exe$\" /S" 
SectionEnd

Section "Uninstall"
!insertmacro SetContext
!insertmacro MUI_STARTMENU_GETFOLDER Startmenu $StartMenuFolder
Delete "$SMPROGRAMS\$StartMenuFolder\@PACKAGE_NAME@.lnk"
Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
RMDir "$SMPROGRAMS\$StartMenuFolder"
@DELETE_LIST@
@RMDIR_LIST@
Delete "$INSTDIR\Uninstall.exe"
RMDir "$INSTDIR"
DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@"
SectionEnd

